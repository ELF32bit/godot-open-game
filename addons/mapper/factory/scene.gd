class_name MapperFactory

var settings: MapperSettings
var game_property_converter: MapperPropertyConverter
var game_loader: MapperLoader

var random_number_generator := RandomNumberGenerator.new()
var progress: float = 0.0
var build_time: int = 0


func _init(settings: MapperSettings) -> void:
	self.settings = settings
	# creating game loader instance
	var game_loader_instance = settings.game_loader.new()
	if game_loader_instance is MapperLoader:
		game_loader = game_loader_instance
		game_loader.settings = settings
	else:
		push_error("Game loader script must extend MapperLoader.")
		self.settings = null
	# creating game property converter instance
	var game_property_converter_instance = settings.game_property_converter.new()
	if game_property_converter_instance is MapperPropertyConverter:
		game_property_converter = game_property_converter_instance
		game_property_converter.game_loader = game_loader_instance
		game_property_converter.settings = settings
	else:
		push_error("Game property converter script must extend MapperPropertyConverter.")
		self.settings = null


func build_map(map: MapperMapResource, wads: Array[MapperWadResource] = []) -> PackedScene:
	if not settings: # validating factory settings if init failed
		push_error("Error building map %s, factory settings are missing." % [map.name])
		return null

	var factory := func(action: Callable, progress: float, comment: String) -> void:
		var time := Time.get_ticks_msec()
		action.call()
		time = Time.get_ticks_msec() - time
		build_time += time
		self.progress = progress / 21.0 # number of build steps
		if settings.print_progress:
			if not settings.print_progress_verbose and time <= 250:
				return
			print("(%.2f) %s: %.3fs" % [self.progress, comment, time / 1000.0])

	var parallel_task := func(action: Callable, elements: int, tasks_needed: int = -1) -> void:
		if tasks_needed != 0 and settings.use_threads:
			var group_task := WorkerThreadPool.add_group_task(action, elements, tasks_needed, true)
			WorkerThreadPool.wait_for_group_task_completion(group_task)
		else:
			for index in range(elements):
				action.call(index)

	# preparing game loader wads and resetting previous build times
	game_loader.custom_wads.assign(wads)
	progress = 0.0
	build_time = 0

	# preparing random number generators
	var map_data_seed := map.source_file.hash() + settings.map_data_seed
	game_loader.random_number_generator.seed = map_data_seed
	random_number_generator.seed = map_data_seed

	# preloading post build script
	var post_build_script: GDScript = null
	if settings.post_build_script_enabled:
		var path := settings.game_builders_directory.path_join(settings.post_build_script_name)
		post_build_script = game_loader.load_script(path)

	# determining if post build script should build faces colors
	var post_build_faces_colors := false
	if settings.post_build_faces_colors_enabled:
		if post_build_script and post_build_script.has_method("build_faces_colors"):
			post_build_faces_colors = true

	# creating scene root
	var packed_scene := PackedScene.new()
	var scene_root := Node3D.new()
	scene_root.name = map.name

	# creating map structure from resource
	var map_structure := MapperMap.new()
	map_structure.name = map.name
	map_structure.source_file = map.source_file
	map_structure.wads.append_array(wads)
	map_structure.factory = self
	map_structure.settings = settings
	map_structure.loader = game_loader
	map_structure.node = scene_root

	# preparing to create other structures from resources
	var face_structures: Array[MapperFace] = []
	var brush_structures: Array[MapperBrush] = []
	var entity_structures: Array[MapperEntity] = map_structure.entities
	var smooth_entity_structures: Array[MapperEntity] = []

	# generating all entity structures
	var _generate_all_entity_structures := func() -> Array[MapperEntity]:
		var all_entity_structures: Array[MapperEntity] = []
		all_entity_structures.resize(map.entities.size())
		for entity_index in range(map.entities.size()):
			var entity := map.entities[entity_index]
			var entity_structure := MapperEntity.new()
			all_entity_structures[entity_index] = entity_structure
			entity_structure.properties = entity.properties.duplicate()
			entity_structure.factory = self
		return all_entity_structures

	# generating map structure groups (only for TB maps)
	var _generate_map_groups := func(all_entity_structures: Array[MapperEntity]) -> void:
		var group_entity_classname := settings.group_entity_classname
		var group_entity_id_property := settings.group_entity_id_property
		var group_entity_type_property := settings.group_entity_type_property
		var group_entity_types := settings.group_entity_types
		for group_entity_type in group_entity_types:
			map_structure.groups[group_entity_type] = {}
		for entity_structure in all_entity_structures:
			var entity_classname = entity_structure.get_classname_property(null)
			if not entity_classname == group_entity_classname:
				continue
			if not group_entity_type_property in entity_structure.properties:
				continue
			var group_entity_type = entity_structure.properties[group_entity_type_property]
			var type_index = group_entity_types.find(group_entity_type)
			var id = entity_structure.get_int_property(group_entity_id_property, null)
			if type_index != -1 and id != null:
				map_structure.groups[group_entity_types[type_index]][id] = entity_structure

	# getting the first world entity and default layer omit from export (only for TB maps)
	var _tb_omit := bool(settings.tb_layer_omit_from_export_enabled and settings.group_entity_enabled)
	var _get_first_world_entity_structure := func(all_entity_structures: Array[MapperEntity]) -> MapperEntity:
		var first_world_entity_structure: MapperEntity = null
		var world_entity_classname := settings.world_entity_classname
		var tb_omit_property := settings.tb_layer_omit_from_export_property
		for entity_structure in all_entity_structures:
			var entity_classname = entity_structure.get_classname_property(null)
			if not entity_classname == world_entity_classname:
				continue
			if first_world_entity_structure == null:
				first_world_entity_structure = entity_structure
			if not _tb_omit:
				return first_world_entity_structure
			if entity_structure.get_int_property(tb_omit_property, 0) != 0:
				first_world_entity_structure.metadata["__tb_omit"] = true
				return first_world_entity_structure
		return first_world_entity_structure

	# skipping entities from TB layers that declare omit from export (only for TB maps)
	var _skip_omit_entities := func(all_entity_structures: Array[MapperEntity], first_world_entity_structure: MapperEntity) -> void:
		var tb_omit_property := settings.tb_layer_omit_from_export_property
		var tb_default_layer_omit := false
		if first_world_entity_structure:
			if first_world_entity_structure.metadata.get("__tb_omit", false):
				tb_default_layer_omit = true
		for entity_index in range(all_entity_structures.size()):
			var entity_structure := all_entity_structures[entity_index]
			var entity_structure_layer := map_structure.get_entity_layer(entity_structure)
			if not entity_structure_layer:
				if tb_default_layer_omit:
					entity_structure.metadata["__tb_omit"] = true
				continue
			if entity_structure_layer.get_int_property(tb_omit_property, 0) != 0:
				entity_structure.metadata["__tb_omit"] = true

	# binding common entity properties, scale can also be bound here
	var _forward_rotation_euler := settings.get_forward_rotation().get_euler()
	var _bind_common_entity_properties := func(entity_structure: MapperEntity) -> void:
		entity_structure.node_properties["rotation"] = _forward_rotation_euler
		entity_structure.bind_origin_property("position")
		entity_structure.bind_angle_property("rotation")
		entity_structure.bind_angles_property("rotation")
		entity_structure.bind_mangle_property("rotation")

	# generating and adding extra brushes to the first world entity
	var _generate_world_entity_structure := func(first_structure: MapperEntity, extra_brush_structures: Array[MapperBrush]) -> void:
		var tb_default_layer_omit := false
		var world_entity_structure: MapperEntity = null
		var world_entity_classname := settings.world_entity_classname
		var is_skipped_entity := settings.is_skip_entity_classname(world_entity_classname)
		if first_structure and first_structure.metadata.get("__tb_omit", false):
			tb_default_layer_omit = true
		# duplicating omit entity or using an existing world entity
		if tb_default_layer_omit and not is_skipped_entity:
			world_entity_structure = MapperEntity.new()
			world_entity_structure.properties = first_structure.properties.duplicate()
			world_entity_structure.brushes.append_array(extra_brush_structures)
			world_entity_structure.factory = self
			# adding new world entity to the map structure classnames dictionary
			if not world_entity_classname in map_structure.classnames:
				map_structure.classnames[world_entity_classname] = []
			map_structure.classnames[world_entity_classname].append(world_entity_structure)
			# binding common properties for the new world entity
			_bind_common_entity_properties.call(world_entity_structure)
			entity_structures.append(world_entity_structure)
		elif not is_skipped_entity:
			world_entity_structure = map_structure.classnames.get(world_entity_classname, [null])[0]
			if world_entity_structure:
				world_entity_structure.brushes.append_array(extra_brush_structures)

#1. Generating map structures
	var generate_structures := func() -> void:
		var all_entity_structures: Array[MapperEntity] = []
		all_entity_structures = _generate_all_entity_structures.call()

		# creating map structure TB groups dictionary
		if settings.group_entity_enabled:
			_generate_map_groups.call(all_entity_structures)

		# getting the first world entity and skipping entities from TB layers
		var first_world_entity_structure: MapperEntity = null
		first_world_entity_structure = _get_first_world_entity_structure.call(all_entity_structures)
		if _tb_omit:
			_skip_omit_entities.call(all_entity_structures, first_world_entity_structure)

		var world_entity_extra_brush_structures: Array[MapperBrush] = []
		var world_entity_extra_brush_entities_classnames: Dictionary = {}
		for classname in settings.world_entity_extra_brush_entities_classnames:
			world_entity_extra_brush_entities_classnames[classname] = true

		for entity_index in range(map.entities.size()):
			var entity := map.entities[entity_index]
			var is_world_entity_extra_brush_entity := false
			var entity_structure := all_entity_structures[entity_index]
			if entity_structure.metadata.get("__tb_omit", false):
				continue

			# skipping certain entities from settings
			var entity_classname = entity_structure.get_classname_property(null)
			var is_skipped_entity := false
			if entity_classname != null:
				is_skipped_entity = settings.is_skip_entity_classname(entity_classname)
			elif settings.skip_entities_enabled and settings.skip_entities_without_classname:
				is_skipped_entity = true

			# creating map structure classnames dictionary
			if not is_skipped_entity and entity_classname != null:
				if not entity_classname in map_structure.classnames:
					map_structure.classnames[entity_classname] = []
				map_structure.classnames[entity_classname].append(entity_structure)

			# marking world entity extra brush entities
			if settings.world_entity_extra_brush_entities_enabled and entity_classname != null:
				if entity_classname in world_entity_extra_brush_entities_classnames:
					if entity_structure != first_world_entity_structure:
						is_world_entity_extra_brush_entity = true

			# obtaining entity lightmap scale and binding common properties
			var entity_lightmap_scale: float = 1.0
			if entity.brushes.size():
				entity_lightmap_scale = entity_structure.get_lightmap_scale_property(1.0)
			_bind_common_entity_properties.call(entity_structure)
			if not is_skipped_entity:
				entity_structures.append(entity_structure)
			elif is_world_entity_extra_brush_entity:
				entity_structure.metadata["_extra_entity"] = true

			for brush in entity.brushes:
				var brush_structure := MapperBrush.new()
				entity_structure.brushes.append(brush_structure)
				if is_world_entity_extra_brush_entity:
					world_entity_extra_brush_structures.append(brush_structure)
					brush_structures.append(brush_structure)
				elif not is_skipped_entity:
					brush_structures.append(brush_structure)
				brush_structure.lightmap_scale = entity_lightmap_scale
				brush_structure.factory = self

				for face in brush.faces:
					var face_structure := MapperFace.new()
					brush_structure.faces.append(face_structure)
					if not is_skipped_entity or is_world_entity_extra_brush_entity:
						face_structures.append(face_structure)

					face_structure.point1 = face.point1.duplicate()
					face_structure.point2 = face.point2.duplicate()
					face_structure.point3 = face.point3.duplicate()
					face_structure.material_name = face.material
					face_structure.u_axis = face.u_axis
					face_structure.v_axis = face.v_axis
					face_structure.uv_shift = face.uv_shift
					face_structure.uv_valve = face.uv_valve
					face_structure.rotation = deg_to_rad(face.rotation)
					face_structure.scale = face.scale
					face_structure.parameters = face.parameters.duplicate()
					face_structure.factory = self

		# adding extra brushes to the first world entity entity
		if settings.world_entity_extra_brush_entities_enabled:
			if world_entity_extra_brush_structures.size() > 0:
				_generate_world_entity_structure.call(first_world_entity_structure,
					world_entity_extra_brush_structures)

		if settings.smooth_shading_property_enabled:
			for entity_structure in entity_structures:
				if entity_structure.is_smooth_shaded():
					smooth_entity_structures.append(entity_structure)
			for entity_structure in all_entity_structures:
				if entity_structure.metadata.get("_extra_entity", false):
					if entity_structure.is_smooth_shaded():
						smooth_entity_structures.append(entity_structure)

#2. Generating faces
	var _texture_suffixes := settings.texture_suffixes.values()
	var generate_faces := func(thread_index: int) -> void:
		var face := face_structures[thread_index]
		var p1 := face.point1.duplicate()
		var p2 := face.point2.duplicate()
		var p3 := face.point3.duplicate()
		var b := settings.basis

		# transforming points coordinates with double precision
		face.point1[0] = b[0][0] * p1[0] + b[1][0] * p1[1] + b[2][0] * p1[2]
		face.point1[1] = b[0][1] * p1[0] + b[1][1] * p1[1] + b[2][1] * p1[2]
		face.point1[2] = b[0][2] * p1[0] + b[1][2] * p1[1] + b[2][2] * p1[2]
		face.point2[0] = b[0][0] * p2[0] + b[1][0] * p2[1] + b[2][0] * p2[2]
		face.point2[1] = b[0][1] * p2[0] + b[1][1] * p2[1] + b[2][1] * p2[2]
		face.point2[2] = b[0][2] * p2[0] + b[1][2] * p2[1] + b[2][2] * p2[2]
		face.point3[0] = b[0][0] * p3[0] + b[1][0] * p3[1] + b[2][0] * p3[2]
		face.point3[1] = b[0][1] * p3[0] + b[1][1] * p3[1] + b[2][1] * p3[2]
		face.point3[2] = b[0][2] * p3[0] + b[1][2] * p3[1] + b[2][2] * p3[2]

		face.plane = Plane(
			Vector3(face.point1[0], face.point1[1], face.point1[2]),
			Vector3(face.point2[0], face.point2[1], face.point2[2]),
			Vector3(face.point3[0], face.point3[1], face.point3[2]))

		# normalizing uv axis vectors and adjusting scale
		if face.uv_valve:
			face.u_axis = settings.basis * face.u_axis
			face.v_axis = settings.basis * face.v_axis
			#face.scale.x *= face.u_axis.length()
			#face.scale.y *= face.v_axis.length()
			#face.u_axis = face.u_axis.normalized()
			#face.v_axis = face.v_axis.normalized()

		# handling division by zero
		if is_zero_approx(face.scale.x):
			face.scale.x = 1.0
		if is_zero_approx(face.scale.y):
			face.scale.y = 1.0

		# removing texture suffixes from material names
		for suffix_index in range(_texture_suffixes.size() - 1, -1, -1):
			var suffix: String = _texture_suffixes[suffix_index]
			if face.material_name.ends_with(suffix):
				face.material_name = face.material_name.trim_suffix(suffix)
				break

		# marking skipped faces
		if settings.skip_material_enabled:
			var material_file := face.material_name.get_file()
			if material_file.matchn(settings.skip_material_name):
				face.skip = true
			else:
				for skip_material_alias in settings.skip_material_aliases:
					if material_file.matchn(skip_material_alias):
						face.skip = true
						break

#3. Generating materials
	var generate_materials := func() -> void:
		for face in face_structures:
			if not face.material_name in map_structure.materials:
				map_structure.materials[face.material_name] = MapperMaterial.new()
			face.material = map_structure.materials[face.material_name]

	# intersecting brush planes in the most precise way possible (without double precision)
	var _generate_brush_faces_precise := func(brush: MapperBrush, epsilon: float) -> void:
		for face1 in brush.faces:
			for face2 in brush.faces:
				for face3 in brush.faces:
					var vertex = face1.plane.intersect_3(face2.plane, face3.plane)
					if vertex != null:
						if brush.has_point(vertex, epsilon):
							if not face1.has_vertex(vertex, epsilon):
								face1.vertices.append(vertex)

	# intersecting brush planes in a faster way for brushes with many faces
	var _generate_brush_faces_fast := func(brush: MapperBrush, epsilon: float) -> void:
		var planes: Array[Plane] = []
		planes.resize(brush.faces.size())
		for face_index in range(brush.faces.size()):
			planes[face_index] = brush.faces[face_index].plane
		var vertices := Geometry3D.compute_convex_mesh_points(planes)
		for face in brush.faces:
			for vertex in vertices:
				if face.plane.has_point(vertex, epsilon):
					if not face.has_vertex(vertex, epsilon):
						face.vertices.append(vertex)

	var _snap_brush_vertices_to_grid := func(brush: MapperBrush) -> void:
		var grid_snap_step := settings.grid_snap_step
		for face in brush.faces:
			var face_vertices := face.vertices
			for index in range(face_vertices.size()):
				face_vertices[index].x = snappedf(face_vertices[index].x, grid_snap_step)
				face_vertices[index].y = snappedf(face_vertices[index].y, grid_snap_step)
				face_vertices[index].z = snappedf(face_vertices[index].z, grid_snap_step)

	var _generate_brush_bounds := func(brush: MapperBrush) -> void:
		var has_vertex := false
		for face in brush.faces:
			for vertex in face.vertices:
				if has_vertex:
					brush.aabb = brush.aabb.expand(vertex)
				else:
					brush.aabb = AABB(vertex, Vector3.ZERO)
					has_vertex = true
				face.center += vertex
			face.center /= face.vertices.size()
			brush.center += face.center
		if brush.faces.size():
			brush.center /= brush.faces.size()

	var _sort_clockwise := func(a: Vector4, b: Vector4) -> bool:
		return a.w > b.w

	var _sort_brush_vertices := func(brush: MapperBrush, sort_function: Callable) -> void:
		for face in brush.faces:
			var wound_vertices: Array[Vector4] = []
			wound_vertices.resize(face.vertices.size())
			var u := (face.vertices[1] - face.vertices[0]).normalized()
			var v := u.cross(face.plane.normal).normalized()
			for index in range(face.vertices.size()):
				var d := face.vertices[index] - face.center
				var uv := Vector2(-d.dot(u), d.dot(v))
				wound_vertices[index] = Vector4(d.x, d.y, d.z, uv.angle())
			wound_vertices.sort_custom(sort_function)
			for index in range(face.vertices.size()):
				var d := wound_vertices[index]
				face.vertices[index] = Vector3(d.x, d.y, d.z) + face.center

	var _scale_brush_coordinates := func(brush: MapperBrush) -> void:
		var scale := (1.0 / settings.unit_size)
		var transform := Transform3D.IDENTITY.scaled(Vector3.ONE * scale)
		for face in brush.faces:
			for index in range(3):
				face.point1[index] *= scale
				face.point2[index] *= scale
				face.point3[index] *= scale
			face.plane.d *= scale
			face.center *= scale
			face.vertices = transform * face.vertices
			face.uv_shift *= scale
		brush.aabb.position *= scale
		brush.aabb.size *= scale
		brush.center *= scale

#4. Generating brushes
	var generate_brushes := func(thread_index: int) -> void:
		var brush := brush_structures[thread_index]
		var epsilon := settings.epsilon

		# finding face vertices forming convex hull by intersecting face planes
		if settings.use_experimental_brush_algorithm:
			_generate_brush_faces_fast.call(brush, epsilon)
		else:
			_generate_brush_faces_precise.call(brush, epsilon)

		# removing brush faces that failed to form triangles
		for index in range(brush.faces.size() - 1, -1, -1):
			if brush.faces[index].vertices.size() < 3:
				brush.faces.remove_at(index)
				brush.is_degenerate = true

		# snapping brush vertices to grid to improve precision
		if settings.grid_snap_enabled:
			_snap_brush_vertices_to_grid.call(brush)

		# creating brush vertex normals from plane normals
		for face in brush.faces:
			var face_normals := face.normals
			face_normals.resize(face.vertices.size())
			face_normals.fill(face.plane.normal)

		# finding brush surfaces and materials
		for face in brush.faces:
			if not face.material_name in brush.surfaces:
				brush.surfaces[face.material_name] = []
			brush.surfaces[face.material_name].append(face)
			brush.materials[face.material_name] = face.material

		_generate_brush_bounds.call(brush)
		_sort_brush_vertices.call(brush, _sort_clockwise)
		_scale_brush_coordinates.call(brush)

	# obtaining unique (non-coplanar) entity faces for smooth entity normals
	var _get_unique_entity_faces := func(entity_faces: Array[MapperFace], epsilon: float) -> Array[bool]:
		var unique_entity_faces: Array[bool] = []
		unique_entity_faces.resize(entity_faces.size())
		unique_entity_faces.fill(true)
		for index1 in range(entity_faces.size()):
			if not unique_entity_faces[index1]:
				continue
			var face1 := entity_faces[index1]
			var face_center1 := face1.center
			var plane_center1 := face1.plane.get_center()
			var face1_vertices_size := face1.vertices.size()
			var face1_vertices := face1.vertices
			for index2 in range(index1 + 1, entity_faces.size()):
				if not unique_entity_faces[index2]:
					continue
				var face2 := entity_faces[index2]
				# quickly checking non-coplanar faces centers and vertices size
				if face1_vertices_size != face2.vertices.size():
					continue
				if not MapperUtilities.is_equal_approximately(face_center1, face2.center, epsilon):
					continue
				if not MapperUtilities.is_equal_approximately(plane_center1, face2.plane.get_center(), epsilon):
					continue
				# slowly checking coplanar faces vertices
				var is_different_face := false
				var face2_vertices := face2.vertices
				for vertex1 in face1_vertices:
					var is_different_vertex := true
					for vertex2 in face2_vertices:
						if MapperUtilities.is_equal_approximately(vertex1, vertex2, epsilon):
							is_different_vertex = false
							break
					if is_different_vertex:
						is_different_face = true
						break
				if is_different_face:
					continue
				unique_entity_faces[index1] = false
				unique_entity_faces[index2] = false
		return unique_entity_faces

#5. Generating smooth entity normals
	var generate_smooth_entity_normals := func(thread_index: int) -> void:
		var entity := smooth_entity_structures[thread_index]
		var epsilon := settings.epsilon / settings.unit_size

		var split_angle_property := settings.smooth_shading_split_angle_property
		var split_angle: float = entity.get_float_property(split_angle_property, 89.0)
		split_angle = deg_to_rad(clampf(split_angle, 0.0, 180.0))

		# obtaining entity faces without skip material
		var entity_faces: Array[MapperFace] = []
		for brush in entity.brushes:
			for face in brush.faces:
				if not face.skip:
					entity_faces.append(face)

		# collecting entity faces, discarding faces in the same plane with the same centers
		var unique_entity_faces = _get_unique_entity_faces.call(entity_faces, epsilon)

		# collecting unique vertices and face normals
		var vertices := PackedVector3Array()
		var indices: Array[PackedInt32Array] = []
		var faces: Array[Array] = []

		for entity_face_index in range(entity_faces.size()):
			if not unique_entity_faces[entity_face_index]:
				continue
			var face := entity_faces[entity_face_index]
			var face_vertices := face.vertices

			for index1 in range(face_vertices.size()):
				var vertex := face_vertices[index1]
				var is_unique_vertex := true

				for index2 in range(vertices.size()):
					var unique_vertex := vertices[index2]
					if MapperUtilities.is_equal_approximately(vertex, unique_vertex, epsilon):
						indices[index2].append(index1)
						faces[index2].append(face)
						is_unique_vertex = false
						break

				if is_unique_vertex:
					vertices.append(vertex)
					indices.append(PackedInt32Array([index1]))
					faces.append([face])

		# calculating smooth normals
		for index1 in range(faces.size()):
			var normals := PackedVector3Array()
			var faces1: Array = faces[index1]
			normals.resize(faces1.size())

			for index2 in range(faces1.size()):
				normals[index2] = faces1[index2].plane.normal

			for index2 in range(faces1.size()):
				var faces12: MapperFace = faces1[index2]
				var faces12_normal: Vector3 = faces12.plane.normal
				for index3 in range(index2 + 1, faces1.size()):
					var faces13: MapperFace = faces1[index3]
					var faces13_normal: Vector3 = faces13.plane.normal
					if faces12_normal.angle_to(faces13_normal) < split_angle:
						normals[index2] += faces13_normal
						normals[index3] += faces12_normal
				var face12_smooth_normal := normals[index2].normalized()
				faces12.normals[indices[index1][index2]] = face12_smooth_normal
				if not faces12_normal.is_equal_approx(face12_smooth_normal):
					faces12.is_smooth_shaded = true

#6. Loading world entity wads, for convenience
	var load_world_entity_wads := func() -> void:
		var game_wads_directory := settings.game_wads_directory
		var world_entity_wads_property := settings.world_entity_wads_property
		for entity in map_structure.classnames.get(settings.world_entity_classname, []):
			if not entity.properties.has(world_entity_wads_property):
				continue
			for path in entity.properties.get(world_entity_wads_property, "").split(";", false):
				var path_strip: String = path.strip_edges()
				var wad_path := game_wads_directory
				if path_strip.is_absolute_path():
					wad_path = wad_path.path_join(path_strip.get_file())
				elif path_strip.is_relative_path():
					wad_path = wad_path.path_join(path_strip.trim_prefix("/"))
				else:
					continue
				var wad: MapperWadResource = null
				if settings.world_entity_wads_palette != null:
					wad = game_loader.load_wad_raw(wad_path, settings.world_entity_wads_palette)
				else:
					wad = game_loader.load_wad(wad_path)
				if wad:
					map_structure.wads.append(wad)

	# enabling base material features to provide visual feedback
	var _enable_base_material_slot_feature := func(material: BaseMaterial3D, slot: int) -> void:
		match slot:
			BaseMaterial3D.TEXTURE_METALLIC:
				material.metallic = 1.0
				material.metallic_specular = 0.5
			BaseMaterial3D.TEXTURE_EMISSION:
				material.set_feature(BaseMaterial3D.FEATURE_EMISSION, true)
			BaseMaterial3D.TEXTURE_NORMAL:
				material.set_feature(BaseMaterial3D.FEATURE_NORMAL_MAPPING, true)
			#BaseMaterial3D.TEXTURE_BENT_NORMAL:
			#	material.set_feature(BaseMaterial3D.FEATURE_BENT_NORMAL_MAPPING, true)
			BaseMaterial3D.TEXTURE_RIM:
				material.set_feature(BaseMaterial3D.FEATURE_RIM, true)
			BaseMaterial3D.TEXTURE_CLEARCOAT:
				material.set_feature(BaseMaterial3D.FEATURE_CLEARCOAT, true)
			BaseMaterial3D.TEXTURE_FLOWMAP:
				material.set_feature(BaseMaterial3D.FEATURE_ANISOTROPY, true)
				material.anisotropy = 1.0
			BaseMaterial3D.TEXTURE_AMBIENT_OCCLUSION:
				material.set_feature(BaseMaterial3D.FEATURE_AMBIENT_OCCLUSION, true)
			BaseMaterial3D.TEXTURE_HEIGHTMAP:
				material.set_feature(BaseMaterial3D.FEATURE_HEIGHT_MAPPING, true)
			BaseMaterial3D.TEXTURE_SUBSURFACE_SCATTERING:
				material.set_feature(BaseMaterial3D.FEATURE_SUBSURFACE_SCATTERING, true)
				material.subsurf_scatter_strength = 1.0
			BaseMaterial3D.TEXTURE_SUBSURFACE_TRANSMITTANCE:
				material.set_feature(BaseMaterial3D.FEATURE_SUBSURFACE_TRANSMITTANCE, true)
			BaseMaterial3D.TEXTURE_BACKLIGHT:
				material.set_feature(BaseMaterial3D.FEATURE_BACKLIGHT, true)
			BaseMaterial3D.TEXTURE_REFRACTION:
				material.set_feature(BaseMaterial3D.FEATURE_REFRACTION, true)
			BaseMaterial3D.TEXTURE_DETAIL_ALBEDO, BaseMaterial3D.TEXTURE_DETAIL_NORMAL:
				material.set_feature(BaseMaterial3D.FEATURE_DETAIL, true)

	var _enable_base_material_features := func(material: BaseMaterial3D) -> void:
		for slot in BaseMaterial3D.TEXTURE_MAX:
			if material.get_texture(slot):
				_enable_base_material_slot_feature.call(material, slot)

#7. Loading materials and textures
	var load_materials_and_textures := func() -> void:
		for material in map_structure.materials:
			var path := settings.game_materials_directory.path_join(material)
			var base_material: BaseMaterial3D = game_loader.load_base_material()
			var override_material: Material = game_loader.load_material(path)
			var properties := settings.override_material_metadata_properties

			# loading base and override materials
			if override_material:
				var is_referenced := false
				if override_material.has_meta("_mapper_reference"):
					if override_material.get_meta("_mapper_reference", false):
						override_material.remove_meta("_mapper_reference")
						is_referenced = true
				if override_material.has_meta("mapper_reference"):
					is_referenced = override_material.get_meta("mapper_reference", false)
				if not is_referenced:
					override_material = override_material.duplicate()

				# also loading physics material from metadata
				var physics_material: PhysicsMaterial = null
				if override_material.has_meta(properties.physics_material):
					physics_material = override_material.get_meta(properties.physics_material, null)
				if physics_material and not is_referenced:
					physics_material = physics_material.duplicate()

				map_structure.materials[material].base = base_material
				map_structure.materials[material].override = override_material
				map_structure.materials[material].physics = physics_material
			else:
				map_structure.materials[material].base = base_material
				map_structure.materials[material].override = null
				map_structure.materials[material].physics = null

			# loading material slot textures
			var slot_textures: Dictionary = {}
			var slot_name_textures: Dictionary = {}
			for slot in settings.shader_texture_slots:
				var slot_name: String = settings.shader_texture_slots[slot]
				var texture_suffix: String = settings.texture_suffixes[slot]

				# trying to load texture or textures for the current texture slot
				path = settings.game_textures_directory.path_join(material + texture_suffix)
				var material_textures := game_loader.load_animated_textures(path, map_structure.wads)
				if not material_textures.get("textures", []).size() and slot == BaseMaterial3D.TEXTURE_ALBEDO:
					path = settings.game_textures_directory.path_join(material)
					material_textures = game_loader.load_animated_textures(path, map_structure.wads)
				# skipping texture slot if no textures were found
				var textures: Array[Texture2D] = material_textures.get("textures", [])
				if not textures.size():
					continue

				var texture_index: int = 0
				if textures.size() > 1:
					slot_textures[slot] = textures
					slot_name_textures[slot_name] = textures
					texture_index = material_textures.get("texture_index", -1)
					if not (texture_index >= 0 and texture_index < textures.size()):
						texture_index = 0

				# setting texture on the base and override materials
				base_material.set_texture(slot, textures[texture_index])
				if override_material:
					if override_material is BaseMaterial3D:
						override_material.set_texture(slot, textures[texture_index])
					elif override_material is ShaderMaterial:
						override_material.set_shader_parameter(slot_name, textures[texture_index])

			# enabling features on base materials if textures are provided
			_enable_base_material_features.call(base_material)
			if override_material is BaseMaterial3D:
				_enable_base_material_features.call(override_material)

			# saving alternative textures to material metadata
			if slot_textures.size():
				var metadata_property := settings.alternative_textures_metadata_property
				base_material.set_meta(metadata_property, slot_textures)
				if override_material:
					if override_material is BaseMaterial3D:
						override_material.set_meta(metadata_property, slot_textures)
					elif override_material is ShaderMaterial:
						override_material.set_meta(metadata_property, slot_name_textures)

	# coloring face vertices with barycentric coordinates (requires central vertex for n-gons)
	var _generate_barycentric_coordinates := func(face: MapperFace, colors: PackedColorArray) -> void:
		colors[0] = Color.RED
		for index in range(1, colors.size()):
			if index % 2 == 1:
				colors[index] = Color.GREEN
			else:
				colors[index] = Color.BLUE
		# disabling inner edges based on face type (triangle, quad, n-gon)
		var face_type: int = 0
		if face.vertices.size() == 4:
			face_type = (2 | face_type)
		elif face.vertices.size() > 4:
			face_type = (2 | 4 | 8 | face_type)
		# disabling wireframes for smooth shaded faces
		if face.is_smooth_shaded:
			face_type = (1 | 2 | 4 | face_type)
		# storing wireframe mode in the colors alpha channel
		for index in range(colors.size()):
			colors[index].a = float(16.0 - face_type) / 16.0

#8. Generating brush geometry
	var _inverse_basis := settings.basis.inverse()
	var generate_brush_geometry := func(thread_index: int) -> void:
		var brush := brush_structures[thread_index]
		var triangles: PackedVector3Array = []
		var unique_points: Dictionary = {}
		var surface_tools: Dictionary = {}
		var is_concave_mesh := false

		# creating surface tools from brush surfaces
		for face in brush.faces:
			var face_vertices := face.vertices
			var face_normal := face.plane.normal
			var normals := face.get_normals(true)
			var vertices := face.get_vertices(brush.center, true)
			for vertex in face.get_vertices(brush.center, false):
				unique_points[vertex] = true

			if face.skip:
				is_concave_mesh = true
				if not settings.skip_material_affects_collision:
					triangles.append_array(face.get_triangles(brush.center, true))
				continue
			triangles.append_array(face.get_triangles(brush.center, true))

			var face_material := face.material
			var material_name := face.material_name
			if not material_name in surface_tools:
				surface_tools[material_name] = SurfaceTool.new()
				surface_tools[material_name].begin(Mesh.PRIMITIVE_TRIANGLES)
				var material: Material = face_material.base
				if not settings.store_base_materials:
					material = face_material.get_material()
				surface_tools[material_name].set_material(material)

			# face base material always exists storing albedo texture
			var texture_size := face.get_texture_size()
			var uvs := PackedVector2Array()
			uvs.resize(vertices.size())
			if vertices.size() != face_vertices.size():
				uvs[0] = face.get_uv(vertices[0] + brush.center, texture_size, _inverse_basis)
				for index in range(1, vertices.size() - 1):
					uvs[index] = face.get_uv(face_vertices[index - 1], texture_size, _inverse_basis)
				uvs[vertices.size() - 1] = uvs[1]
			else:
				for index in range(vertices.size()):
					uvs[index] = face.get_uv(face_vertices[index], texture_size, _inverse_basis)

			var colors := PackedColorArray()
			colors.resize(vertices.size())
			colors.fill(Color.WHITE)

			# storing barycentric coordinates as face vertex colors
			if settings.store_barycentric_coordinates:
				_generate_barycentric_coordinates.call(face, colors)

			# applying post colors to face vertices
			var should_triangulate := false
			if post_build_faces_colors:
				var colors_size := colors.size()
				post_build_script.call("build_faces_colors", face, colors)
				if colors_size > 3 and colors.size() == (colors_size - 2) * 3:
					should_triangulate = true
				elif colors.size() != colors_size:
					push_warning("Failed setting face colors, array is resized!")
					colors.resize(colors_size)
					colors.fill(Color.WHITE)

			# adding triangle fan to the current surface tool
			if should_triangulate:
				for index in range(1, vertices.size() - 1):
					surface_tools[material_name].add_triangle_fan(
						[vertices[0], vertices[index], vertices[index + 1]],
						[uvs[0], uvs[index], uvs[index + 1]],
						[colors[index * 3 - 3], colors[index * 3 - 2], colors[index * 3 - 1]],
						[],
						[normals[0], normals[index], normals[index + 1]],
						[])
			else:
				surface_tools[material_name].add_triangle_fan(
					vertices, uvs, colors, [], normals, [])

		# indexing brush surface tools and also generating tangents
		for material in surface_tools:
			surface_tools[material].index()
			surface_tools[material].generate_tangents()

		# creating brush array mesh from surface tools
		if surface_tools.size() > 0:
			brush.mesh = ArrayMesh.new()
			for material in surface_tools:
				brush.mesh = surface_tools[material].commit(brush.mesh)
				# material surface names are required for override materials
				var surface_index := brush.mesh.get_surface_count() - 1
				brush.mesh.surface_set_name(surface_index, material)
		if settings.shadow_meshes and settings.brush_shadow_meshes:
			if brush.mesh and not settings.use_threads:
				MapperUtilities.generate_shadow_mesh(brush.mesh)

		# creating brush collision shapes
		if not brush.is_degenerate and unique_points.size() > 0:
			brush.convex_shape = ConvexPolygonShape3D.new()
			brush.convex_shape.set_points(unique_points.keys())
			brush.shape = brush.convex_shape
		if triangles.size() > 0:
			brush.concave_shape = ConcavePolygonShape3D.new()
			brush.concave_shape.set_faces(triangles)
			if brush.is_degenerate:
				brush.shape = brush.concave_shape

		if settings.skip_material_enabled:
			if settings.skip_material_affects_collision:
				if brush.concave_shape and is_concave_mesh:
					brush.shape = brush.concave_shape

	var _generate_brush_occluder := func(mesh: ArrayMesh) -> ArrayOccluder3D:
		var surface_tool := SurfaceTool.new()
		surface_tool.begin(Mesh.PRIMITIVE_TRIANGLES)
		for surface_index in range(mesh.get_surface_count()):
			surface_tool.append_from(mesh, surface_index, Transform3D.IDENTITY)
		var arrays := surface_tool.commit_to_arrays()
		if arrays[Mesh.ARRAY_VERTEX] and arrays[Mesh.ARRAY_INDEX]:
			var occluder := ArrayOccluder3D.new()
			occluder.set_arrays(arrays[Mesh.ARRAY_VERTEX], arrays[Mesh.ARRAY_INDEX])
			return occluder
		return null

#9. Generating brush occluders
	var generate_brush_occluders := func(thread_index: int) -> void:
		var brush := brush_structures[thread_index]
		if brush.mesh:
			brush.occluder = _generate_brush_occluder.call(brush.mesh)

#10. Generating brush lightmap uvs
	var generate_brush_lightmap_uvs := func(thread_index: int) -> void:
		var brush := brush_structures[thread_index]
		var transform := Transform3D.IDENTITY.translated(brush.center)
		if brush.mesh:
			MapperUtilities.lightmap_unwrap(brush.mesh,
				transform, settings.lightmap_texel_size / brush.lightmap_scale)

#11. Generating entity bounds
	var generate_entity_bounds := func(thread_index: int) -> void:
		var entity := entity_structures[thread_index]
		var aabb_is_empty := true
		for brush in entity.brushes:
			if not brush.aabb.has_surface():
				continue
			if aabb_is_empty:
				entity.aabb = brush.aabb
				aabb_is_empty = false
			else:
				entity.aabb = entity.aabb.merge(brush.aabb)
		if aabb_is_empty:
			var origin: Vector3 = entity.get_origin_property(Vector3.ZERO)
			entity.aabb = AABB(origin, Vector3.ZERO)
		entity.center = entity.aabb.get_center()

#12. Generating entity meshes
	var generate_entity_meshes := func(thread_index: int) -> void:
		var properties := settings.override_material_metadata_properties
		var entity := entity_structures[thread_index]
		var surface_tools: Dictionary = {}

		var has_cast_shadow_mesh := false
		var cast_shadow_surface_tool := SurfaceTool.new()
		cast_shadow_surface_tool.begin(Mesh.PRIMITIVE_TRIANGLES)
		if settings.cast_shadow_meshes:
			var has_cast_shadow_brushes := false
			for brush in entity.brushes:
				if not brush.mesh:
					continue
				if brush.get_uniform_property(properties.mesh_disabled, false):
					continue
				if brush.get_uniform_property(properties.cast_shadow, true):
					has_cast_shadow_brushes = true
				elif has_cast_shadow_brushes:
					has_cast_shadow_mesh = true
					break
			if not has_cast_shadow_brushes and entity.brushes.size() > 0:
				entity.metadata["_cast_shadow"] = false

		for brush in entity.brushes:
			if not brush.mesh:
				continue
			if brush.get_uniform_property(properties.mesh_disabled, false):
				continue
			var offset := Transform3D.IDENTITY.translated(brush.center - entity.center)
			for surface_index in range(brush.mesh.get_surface_count()):
				var surface_name := brush.mesh.surface_get_name(surface_index)
				var surface_material := brush.mesh.surface_get_material(surface_index)
				if not surface_name in surface_tools:
					surface_tools[surface_name] = SurfaceTool.new()
					surface_tools[surface_name].begin(Mesh.PRIMITIVE_TRIANGLES)
					surface_tools[surface_name].set_material(surface_material)
				surface_tools[surface_name].append_from(brush.mesh, surface_index, offset)
				if has_cast_shadow_mesh:
					if brush.get_uniform_property(properties.cast_shadow, true):
						cast_shadow_surface_tool.append_from(brush.mesh, surface_index, offset)

		if surface_tools.size():
			entity.mesh = ArrayMesh.new()
			for material in surface_tools:
				entity.mesh = surface_tools[material].commit(entity.mesh)
				# material surface names are required for override materials
				var surface_index := entity.mesh.get_surface_count() - 1
				entity.mesh.surface_set_name(surface_index, material)
		if settings.shadow_meshes and entity.mesh:
			MapperUtilities.generate_shadow_mesh(entity.mesh)

		if has_cast_shadow_mesh:
			var is_valid_mesh := false
			var arrays := cast_shadow_surface_tool.commit_to_arrays()
			var required_arrays := [Mesh.ARRAY_VERTEX, Mesh.ARRAY_INDEX]
			if settings.lightmap_unwrap:
				required_arrays.insert(1, Mesh.ARRAY_NORMAL)
			for index in range(Mesh.ARRAY_MAX):
				if index in required_arrays:
					if arrays[index] != null:
						if arrays[index].size() != 0:
							is_valid_mesh = true
						else:
							arrays[index] = null
				elif arrays[index] != null:
					arrays[index] = null
			if is_valid_mesh:
				entity.cast_shadow_mesh = ArrayMesh.new()
				entity.cast_shadow_mesh.add_surface_from_arrays(
					Mesh.PRIMITIVE_TRIANGLES, arrays)

#13. Generating entity shapes
	var generate_entity_shapes := func(thread_index: int) -> void:
		var properties := settings.override_material_metadata_properties
		var entity := entity_structures[thread_index]
		var triangles: PackedVector3Array = []
		var points: PackedVector3Array = []
		var potentially_convex := false
		var shapes_amount: int = 0

		for brush in entity.brushes:
			if not brush.shape or not brush.concave_shape:
				continue
			if brush.get_uniform_property(properties.collision_disabled, false):
				continue
			shapes_amount += 1
			var offset := Transform3D.IDENTITY.translated(brush.center - entity.center)
			if shapes_amount == 1 and brush.shape == brush.convex_shape:
				points = offset * brush.convex_shape.get_points()
				potentially_convex = true
			var brush_triangles := brush.concave_shape.get_faces()
			triangles.append_array(offset * brush_triangles)

		if shapes_amount > 0:
			entity.concave_shape = ConcavePolygonShape3D.new()
			entity.concave_shape.set_faces(triangles)
			entity.shape = entity.concave_shape
		if shapes_amount == 1 and potentially_convex:
			entity.convex_shape = ConvexPolygonShape3D.new()
			entity.convex_shape.set_points(points)
			entity.shape = entity.convex_shape

#14. Generating entity occluders
	var generate_entity_occluders := func(thread_index: int) -> void:
		var properties := settings.override_material_metadata_properties
		var entity := entity_structures[thread_index]
		var vertices := PackedVector3Array()
		var indices := PackedInt32Array()

		for brush in entity.brushes:
			if not brush.occluder:
				continue
			if brush.get_uniform_property(properties.occluder_disabled, false):
				continue
			var last_vertices_size := vertices.size()
			vertices.append_array(brush.occluder.vertices)
			for index in range(last_vertices_size, vertices.size()):
				vertices[index] = vertices[index] + brush.center - entity.center
			var last_indices_size := indices.size()
			indices.append_array(brush.occluder.indices)
			if last_vertices_size != 0:
				for index in range(last_indices_size, indices.size()):
					indices[index] += last_vertices_size
		if vertices.size() and indices.size():
			entity.occluder = ArrayOccluder3D.new()
			entity.occluder.set_arrays(vertices, indices)

#15. Generating entity lightmap uvs
	var generate_entity_lightmap_uvs := func(thread_index: int) -> void:
		var entity := entity_structures[thread_index]
		var transform := Transform3D.IDENTITY.translated(entity.center)
		var lightmap_scale: float = entity.get_lightmap_scale_property(1.0)
		if entity.mesh:
			MapperUtilities.lightmap_unwrap(entity.mesh,
				transform, settings.lightmap_texel_size / lightmap_scale)
		if entity.cast_shadow_mesh:
			MapperUtilities.lightmap_unwrap(entity.cast_shadow_mesh,
				transform, settings.lightmap_texel_size / lightmap_scale)

#16. Generating entity nodes
	var generate_entity_nodes := func() -> void:
		for classname in map_structure.classnames:
			if settings.post_build_script_enabled:
				if classname == settings.post_build_script_name:
					continue

			var class_builder: GDScript = null
			var path := settings.game_builders_directory.path_join(classname)
			class_builder = game_loader.load_script(path)
			if not (class_builder and class_builder.has_method("build")):
				continue

			var class_root := Node3D.new()
			class_root.name = classname
			scene_root.add_child(class_root, settings.readable_node_names)
			for entity in map_structure.classnames[classname]:
				entity.node = class_builder.call("build", map_structure, entity)

		# setting bound entity properties and groups after creating all nodes
		for entity in map_structure.entities:
			if not entity.node:
				continue

			# also checking node name property before setting
			for node_property in entity.node_properties:
				if node_property == "name":
					var name: Variant = entity.node_properties[node_property]
					if typeof(name) in [TYPE_STRING, TYPE_STRING_NAME]:
						if name.validate_node_name().strip_edges().is_empty():
							continue
				entity.node.set(node_property, entity.node_properties[node_property])
			for group_name in entity.node_groups:
				entity.node.add_to_group(group_name, true)

#17. Generating scene tree
	var generate_scene_tree := func() -> void:
		for classname in map_structure.classnames:
			var class_root := scene_root.get_node_or_null(classname)
			for entity in map_structure.classnames[classname]:
				if not entity.node:
					continue

				# always using readable names for manually set node names
				if entity.parent and entity.parent.node:
					MapperUtilities.add_global_child(entity.node, entity.parent.node, settings)
				elif class_root:
					class_root.add_child(entity.node, settings.readable_node_names)

		# executing post build script after generating scene tree
		if post_build_script and post_build_script.has_method("build"):
			post_build_script.call("build", map_structure)

	var _generate_entity_node_paths := func(entity: MapperEntity) -> void:
		for entity_node in entity.node_paths:
			if not (entity_node is Array and entity_node.size() == 5):
				continue
			var destination_property: StringName = entity_node[0]
			var source_property: StringName = entity_node[1]
			var node_property: StringName = entity_node[2]
			var classname: StringName = entity_node[3]
			var is_first_node: bool = entity_node[4]

			map_structure.bind_target_source_property(source_property)
			if not destination_property in entity.properties:
				continue

			var source_entities = map_structure.target_sources[source_property]
			var destination_entities = entity.properties[destination_property]
			var target_entities = source_entities.get(destination_entities, [])

			if is_first_node:
				for map_entity in target_entities:
					if not map_entity.node:
						continue
					if map_entity.get_classname_property("").match(classname):
						var node_path := entity.node.get_path_to(map_entity.node)
						entity.node.set(node_property, node_path)
						break
			else:
				var new_node_paths: Array[NodePath] = []
				var node_paths: Variant = entity.node.get(node_property)
				if node_paths != null and node_paths is Array[NodePath]:
					new_node_paths.append_array(node_paths)
				else:
					continue

				for map_entity in target_entities:
					if not map_entity.node:
						continue
					if map_entity.get_classname_property("").match(classname):
						var node_path := entity.node.get_path_to(map_entity.node)
						if not node_path in new_node_paths:
							new_node_paths.append(node_path)
				entity.node.set(node_property, new_node_paths)

#18. Generating scene node paths
	var generate_scene_node_paths := func() -> void:
		for entity in entity_structures:
			if entity.node:
				_generate_entity_node_paths.call(entity)

	var _generate_entity_signals := func(entity: MapperEntity) -> void:
		for signal_parameters in entity.signals:
			if not (signal_parameters is Array and signal_parameters.size() == 6):
				continue
			var destination_property: StringName = signal_parameters[0]
			var source_property: StringName = signal_parameters[1]
			var signal_name: StringName = signal_parameters[2]
			var method: StringName = signal_parameters[3]
			var classname: String = signal_parameters[4]
			var flags: int = signal_parameters[5]

			map_structure.bind_target_source_property(source_property)
			if not entity.node.has_signal(signal_name):
				continue
			if not destination_property in entity.properties:
				continue

			var source_entities = map_structure.target_sources[source_property]
			var destination_entities = entity.properties[destination_property]
			var target_entities = source_entities.get(destination_entities, [])

			for map_entity in target_entities:
				if not map_entity.node:
					continue
				if not map_entity.node.has_method(method):
					continue
				if not map_entity.get_classname_property("").match(classname):
					continue

				var callable := Callable(map_entity.node, method)
				if not entity.node.is_connected(signal_name, callable):
					if entity.node.connect(signal_name, callable, flags | CONNECT_PERSIST) != OK:
						push_warning("Failed connecting signal, something is wrong!")

#19. Generating scene signals
	var generate_scene_signals := func() -> void:
		for entity in entity_structures:
			if entity.node:
				_generate_entity_signals.call(entity)

#20. Setting scene tree owner
	var set_scene_tree_owner := func() -> void:
		for node in scene_root.find_children("*", "", true, false):
			if not node.owner or node.owner.scene_file_path.is_empty():
				node.set_owner(scene_root)

#21. Packing scene tree
	var pack_scene_tree := func() -> void:
		var error := packed_scene.pack(scene_root)
		# deleting scene tree from memory
		scene_root.free()
		if error != OK:
			push_error("Error packing scene tree, something is wrong!")
			packed_scene = null

	if settings.print_progress:
		print("Starting building map %s" % [map.name])
	factory.call(generate_structures, 1, "Generating structures")
	factory.call(parallel_task.bind(generate_faces, face_structures.size(), 2), 2, "Generating faces")
	factory.call(generate_materials, 3, "Generating materials")
	factory.call(parallel_task.bind(generate_brushes, brush_structures.size(), 4), 4, "Generating brushes")

	if settings.warn_about_degenerate_brushes:
		var degenerate_brush_structure_amount: int = 0
		for brush_structure in brush_structures:
			if brush_structure.is_degenerate:
				degenerate_brush_structure_amount += 1
		if degenerate_brush_structure_amount > 0:
			push_warning("Found %s degenerate brushes, consider adjusting epsilon." % [degenerate_brush_structure_amount])

	if settings.smooth_shading_property_enabled:
		factory.call(parallel_task.bind(generate_smooth_entity_normals, smooth_entity_structures.size(), 4), 5, "Generating smooth entity normals")

	if settings.world_entity_wads_property_enabled:
		factory.call(load_world_entity_wads, 6, "Loading world entity wads")

	factory.call(load_materials_and_textures, 7, "Loading materials and textures")
	factory.call(parallel_task.bind(generate_brush_geometry, brush_structures.size(), 4), 8, "Generating brush geometry")
	factory.call(parallel_task.bind(generate_entity_bounds, entity_structures.size(), 0), 9, "Generating entity bounds")

	if settings.merge_entity_brushes:
		factory.call(parallel_task.bind(generate_entity_meshes, entity_structures.size(), 0), 10, "Generating entity meshes")
		factory.call(parallel_task.bind(generate_entity_shapes, entity_structures.size(), 2), 11, "Generating entity shapes")

	if settings.occlusion_culling:
		factory.call(parallel_task.bind(generate_brush_occluders, brush_structures.size(), 0), 12, "Generating brush occluders")
	if settings.occlusion_culling and settings.merge_entity_brushes:
		factory.call(parallel_task.bind(generate_entity_occluders, entity_structures.size(), 0), 13, "Generating entity occluders")

	if settings.lightmap_unwrap and settings.brush_lightmap_unwrap:
		factory.call(parallel_task.bind(generate_brush_lightmap_uvs, brush_structures.size(), 0), 14, "Unwrapping brushes for lightmaps")
	if settings.lightmap_unwrap and settings.merge_entity_brushes:
		factory.call(parallel_task.bind(generate_entity_lightmap_uvs, entity_structures.size(), 0), 15, "Unwrapping entities for lightmaps")

	factory.call(generate_entity_nodes, 16, "Generating entity nodes")
	factory.call(generate_scene_tree, 17, "Generating scene tree")
	factory.call(generate_scene_node_paths, 18, "Generating scene node paths")
	factory.call(generate_scene_signals, 19, "Generating scene signals")
	factory.call(set_scene_tree_owner, 20, "Preparing to pack scene tree")
	factory.call(pack_scene_tree, 21, "Packing scene tree")
	if settings.print_progress:
		print("Finished building map %s in %.3fs" % [map.name, (build_time / 1000.0)])

	# clearing out some leftover data
	game_loader.custom_wads.clear()
	game_loader.animated_texture_cache.clear()
	game_loader.wad_cache.clear()
	game_loader.map_cache.clear()
	game_loader.mdl_cache.clear()

	return packed_scene


func build_mdl(mdl: MapperMdlResource) -> PackedScene:
	var packed_scene := PackedScene.new()
	var scene_root := Node3D.new()
	scene_root.name = mdl.name

	# obtaining normal table for mdl file
	var compressed_normals := MapperMdlResource.create_default_compressed_normals()

	# creating simple mdl material with first texture
	var material := game_loader.load_base_material()
	if mdl.textures.size():
		material.albedo_texture = mdl.textures[settings.mdls_skin % mdl.textures.size()]
		material.set_meta(settings.mdls_skins_metadata_property, mdl.textures)
	material.cull_mode = BaseMaterial3D.CULL_DISABLED

	var animation_nodes: Array[Node3D] = []
	var animations: Dictionary = {}

	# creating mdl frame mesh instance
	var create_frame := func(frame: Dictionary, parent: Node3D) -> void:
		var vertices: PackedVector3Array = []
		var compressed_vertices: PackedInt32Array = frame["vertices"]
		vertices.resize(compressed_vertices.size() / 4)
		var normals: PackedVector3Array = []
		normals.resize(vertices.size())

		for index in range(0, compressed_vertices.size(), 4):
			var transformed_vertex := Vector3.ZERO
			transformed_vertex.x = compressed_vertices[index + 0]
			transformed_vertex.y = compressed_vertices[index + 1]
			transformed_vertex.z = compressed_vertices[index + 2]
			vertices[index / 4] = transformed_vertex * mdl.scale + mdl.translation
			normals[index / 4] = compressed_normals[compressed_vertices[index + 3]]

		for index in range(vertices.size()):
			vertices[index] = settings.basis * vertices[index] * (1.0 / settings.unit_size)
			normals[index] = (settings.basis * normals[index]).normalized()

		var surface_tool := SurfaceTool.new()
		surface_tool.begin(Mesh.PRIMITIVE_TRIANGLES)
		surface_tool.set_material(material)
		for index in range(0, mdl.triangles.size(), 4):
			var is_front_face := mdl.triangles[index + 0]
			var index1 := mdl.triangles[index + 1]
			var index2 := mdl.triangles[index + 2]
			var index3 := mdl.triangles[index + 3]

			var triangle_vertices: PackedVector3Array = []
			triangle_vertices.resize(3)
			triangle_vertices[0] = vertices[index1]
			triangle_vertices[1] = vertices[index2]
			triangle_vertices[2] = vertices[index3]

			var triangle_normals: PackedVector3Array = []
			triangle_normals.resize(3)
			triangle_normals[0] = normals[index1]
			triangle_normals[1] = normals[index2]
			triangle_normals[2] = normals[index3]

			var triangle_uvs: PackedVector2Array = []
			for uv_index in [index1, index2, index3]:
				var is_on_seam := mdl.texture_coordinates[uv_index * 3 + 0]
				var u := float(mdl.texture_coordinates[uv_index * 3 + 1]) / mdl.texture_size.x
				var v := float(mdl.texture_coordinates[uv_index * 3 + 2]) / mdl.texture_size.y
				triangle_uvs.append(Vector2(u + 0.5 * int(is_on_seam and not is_front_face), v))

			surface_tool.add_triangle_fan(
				triangle_vertices, triangle_uvs, [], [], triangle_normals, [])
		surface_tool.index()
		surface_tool.generate_tangents()
		surface_tool.optimize_indices_for_cache()

		var mesh_instance := MeshInstance3D.new()
		mesh_instance.mesh = surface_tool.commit()
		if settings.shadow_meshes and mesh_instance.mesh:
			MapperUtilities.generate_shadow_mesh(mesh_instance.mesh)
		parent.add_child(mesh_instance, true)
		mesh_instance.owner = scene_root

		var frame_name: String = frame.get("name", "")
		if not frame_name.is_empty():
			var animation_name := frame_name.rstrip("0123456789")
			var animation_frame := frame_name.trim_prefix(animation_name)
			if animation_frame.is_empty():
				animation_frame = "1"
			if not animation_name.is_empty():
				if not animation_name in animations:
					animations[animation_name] = {}
				animations[animation_name][animation_frame.to_int()] = mesh_instance
			mesh_instance.name = frame_name
		animation_nodes.append(mesh_instance)

	# creating simple animation table for mdl frames
	var create_animations := func() -> void:
		var animation_player := AnimationPlayer.new()
		scene_root.add_child(animation_player, true)
		scene_root.move_child(animation_player, 0)
		animation_player.root_node = animation_player.get_path_to(scene_root)
		var animation_player_root := scene_root
		animation_player.owner = scene_root

		var animation_library := AnimationLibrary.new()
		animation_player.add_animation_library("", animation_library)
		animation_player.autoplay = "RESET"

		if not settings.mdls_autoplay.is_empty() and settings.mdls_autoplay in animations:
			animation_player.autoplay = settings.mdls_autoplay
			if animations[settings.mdls_autoplay].size():
				for node in animation_nodes:
					node.visible = false
				animations[settings.mdls_autoplay][1].visible = true

		for animation_name in animations:
			var animation_frames: Array = animations[animation_name].keys()
			if not animation_frames.size():
				continue
			animation_frames.sort()

			var animation := Animation.new()
			var first_frame: int = animation_frames[0]
			var last_frame: int = animation_frames[-1]
			var frames := float(last_frame - first_frame + int(first_frame != last_frame))
			animation.length = frames * settings.mdls_frame_duration
			animation.loop_mode = Animation.LOOP_LINEAR

			for animation_node in animation_nodes:
				var track_index := animation.add_track(Animation.TYPE_VALUE)
				animation.value_track_set_update_mode(track_index, Animation.UPDATE_DISCRETE)
				var track_path := "%s:visible" % [animation_player_root.get_path_to(animation_node)]
				animation.track_set_path(track_index, track_path)
				animation.track_set_imported(track_index, true)

				for frame in animation_frames:
					var key_time := float(frame - first_frame) * settings.mdls_frame_duration
					var key_value: bool = (animations[animation_name][frame] == animation_node)
					animation.track_insert_key(track_index, key_time, key_value)

			MapperUtilities.remove_repeating_animation_keys(animation)
			animation_library.add_animation(animation_name, animation)
		MapperUtilities.create_reset_animation(animation_player, animation_library)

	# creating mdl frames and animations
	for frame in mdl.frames:
		if frame["type"] > 0:
			var group_node := Node3D.new()
			scene_root.add_child(group_node, true)
			group_node.owner = scene_root
			for group_frame in frame["frames"]:
				create_frame.call(group_frame, group_node)
		else:
			create_frame.call(frame, scene_root)
	create_animations.call()

	# packing scene tree
	var error := packed_scene.pack(scene_root)
	scene_root.free()
	if error != OK:
		push_error("Error packing scene tree, something is wrong!")
		packed_scene = null

	return packed_scene
